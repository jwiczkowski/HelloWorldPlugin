import io.github.httpbuilderng.http.*
import groovyx.net.http.*
import static groovyx.net.http.MultipartContent.multipart

plugins {
    id "io.github.http-builder-ng.http-plugin" version "0.1.1"
    id "java"
}

group 'plenty.base.plugin.hello.world'
version '1.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compileOnly 'com.github.plentymarkets:plentyBase-plugin-dependencies:1.+'

    // https://mvnrepository.com/artifact/javax.servlet/servlet-api
    compile group: 'javax.servlet', name: 'servlet-api', version: '2.5'

}

task fatJar(type: Jar) {
    //classifier = 'all'

    // Name of plugin file
    //baseName = project.name + '-all'

    // Set destination path of jar file. Operating system dependent
    //destinationDir = file(System.getProperty("user.home") + "/Library/Caches/eu.plentymarkets.plentybase/Plugins/dev")

    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version,
                'Main-Class': 'PluginStart'
    }

    // Include resources (plugin.json) in jar
    into 'resources', {
        from 'resources'
    }

    // Include compile dependencies in jar
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

task installPlugin(type: HttpTask) {

    config {
        request.uri = 'https://local.plentybase.de:7331'
    }

    post {
        request.uri.path = '/plugin/install'
        request.contentType = 'multipart/form-data'
        request.headers['Authorization'] = 'XYZ'
        request.body = multipart {
            field 'activated', 'true'
            part 'plugin', jar.archivePath.name, 'application/java-archive', jar.archivePath
        }

        request.encoder 'multipart/form-data', OkHttpEncoders.&multipart
        request.encoder('application/java-archive') {ChainedHttpConfig config, ToServer req->
            req.toServer(new ByteArrayInputStream(jar.archivePath.bytes))}
        response.success {
            println "Successful"
        }
    }
}